buildscript {

    repositories {
        mavenCentral()
        jcenter()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }

    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.4'
        classpath "io.freefair.gradle:lombok-plugin:3.5.2"
    }
}

apply plugin: 'idea'

allprojects {
    group = 'org.openweathermap.java-api'
    version '1.2.2-SNAPSHOT'
    ext {
        sourceCompatibility = JavaVersion.VERSION_1_7
        targetCompatibility = JavaVersion.VERSION_1_7
        sourceEncoding = 'UTF-8'
        buildTime = new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")

        commonDependencies = [
                cglib       : dependencies.create('cglib:cglib-nodep:3.2.12'),
                groovy      : dependencies.create('org.codehaus.groovy:groovy-all:2.5.7'),
                junit       : dependencies.create('junit:junit:4.12'),
                spock       : dependencies.create('org.spockframework:spock-core:1.3-groovy-2.5', {
                    exclude module: 'groovy-all'
                    exclude module: 'junit'
                }),
                gson        : dependencies.create('com.google.code.gson:gson:2.8.5'),
                commons_lang: dependencies.create('org.apache.commons:commons-lang3:3.9'),
                lombok      : dependencies.create('org.projectlombok:lombok:1.18.8'),
                objenesis   : dependencies.create('org.objenesis:objenesis:3.0.1'),
                betamax     : dependencies.create('software.betamax:betamax-junit:2.0.1')
        ]
    }

    repositories {
        mavenCentral()
        jcenter()
        mavenLocal()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }

    apply plugin: "io.freefair.lombok"
}
